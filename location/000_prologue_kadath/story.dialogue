# ===============
#  Plot
# ===============
~ narration_1
"Three times Randolph Carter dreamed of the marvellous city, and three times was he snatched away while still he paused on the high terrace above it."
=> END

~ narration_2
"He resolved to find the gods on unknown Kadath in the cold waste, wherever that might be, and to win from them the sight and remembrance and shelter of the marvellous sunset city."
=> END

~ entrance_ghoul1
Ghoul: Ough! Not how I imagine arrival...
=> END

~ entrance_ghoul2
Ghoul: Ugh... What happen? Where others?
=> END

~ entrance_pickman
Pickman: Everyone else is gone...
=> END

~ entrance_carter_1
Carter: At last, we have arrived. Let us finish this.
=> END

~ entrance_carter_2
Pickman: Still want to do this?
=> END

~ entrance_carter_3
Carter: I must.
=> END

~ starting_instructions
Move around with [color=cyan][{ui_up}]/[{ui_left}]/[{ui_down}]/[{ui_right}][/color]. You can interact with individuals and objects with [color=cyan][{ui_accept}][/color], and cancel with [color=cyan][{ui_cancel}][/color]. Run with [color=cyan][{action_run}][/color].
=> END

~ idle_pickman
Pickman: Go ahead. I'll join you once I've made sure these guys are OK. If you're in a hurry, use [color=cyan][{action_run}][/color] to move faster.
=> END 

~ idle_left_ghoul
if EntitiesService.party.is_unlocked(PartyMember.Id.PICKMAN):
	Ghoul: We stay here. If you disoriented, open menu with [color=cyan][{ui_menu}][/color] and check or change controls.
else:
	Ghoul: Everything still spinning... If you disoriented, open menu with [color=cyan][{ui_menu}][/color] and check or change controls.
=> END

~ idle_right_ghoul
if EntitiesService.party.is_unlocked(PartyMember.Id.PICKMAN):
	Ghoul: Maybe others come. We wait for them. Be careful... Remember you can open menu with [color=cyan][{ui_menu}][/color] and use items for heal. Don't let [color=cyan]HP[/color] or [color=cyan]SP[/color] reach zero!
else:
	Ghoul: One second we riding, next we thrown here. If you hurt, you can open menu with [color=cyan][{ui_menu}][/color] and use items for heal. Don't let [color=cyan]HP[/color] or [color=cyan]SP[/color] reach zero!
=> END 

~ pickman_catch_up
Pickman: Wait!
=> END

~ pickman_recruit
Pickman: Looks like we're not alone.
Pickman: The others will stay at the entrance, but you'll have a better chance with me by your side.
=> END

~ pickman_join
Pickman joined your party!
=> END

~ after_boss
Carter: Let's go.
=> END

~ gods_callout
Carter: Gods of Earth! Cease your hiding and grant me passage to the Sunset City!
=> END

~ pickman_warning
Pickman: Wait. Something's wrong. The Other Gods, their messeng-
=> END

~ carter_call
Carter: Pickman!
=> END

~ nyarlathotep_1
????: Randolph Carter.
=> END

~ nyarlathotep_2
????: You have come to see the Great Ones, whom it is unlawful for men to see. 
????: Watchers have spoken of this thing, and the Other Gods have grunted as they rolled and tumbled mindlessly to the sound of thin flutes in the black ultimate void, where broods the daemon-sultan whose name no lips dare speak aloud.
????: Because of YOU, Earth has no longer any gods that are gods.
=> END

~ nyarlathotep_3
????: [shake rate=20.0 level=5 connected=1]And only the Other Gods from outer space hold sway on unremembered Kadath.[/shake]
=> END

# ===============
#  Interactions
# ===============

~ lamp
Though there is no wind in here, these eery flames continuously dance and flicker.
=> END!

~ cauldron
The halls are permeated by a sort of phosphorescence. These lamps are purely for decorative purposes. Perhaps ritualistic.
=> END!

~ statue_disabled
A statue with dog-like twin heads, holding an amphora. 
=> END!

~ statue_enabled
A double-headed statue. Its amphora is pouring a suspicious red liquid.
=> END!

~ acolyte_remains
Blood-stained clothes. All that remains of a willing participant in a ritual sacrifice.
=> END!

~ pushing_barrier
This is not the time to retreat. The journey to this place has been long and hard, and it's time to face the gods.
=> END

~ barrier
Carter: An abyss leading to unknown depths. These statues, though, seem to be connected to the adjacent rooms.
=> END!

~ chalice
There is a curious glyph on the chalice. It seems to be pulsing with some sort of energy.

Touch the glyph?
- Yes
	do DialogueService.add_response("yes")
	You lean forward and touch the glyph.
- No
	do DialogueService.add_response("no")
	Maybe later.
=> END

~ after_chalice_activation
Something seems to have changed.
=> END

~ chalice_done
The chalice is filled with a red liquid.
=> END
