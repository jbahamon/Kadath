# ===============
#  Plot
# ===============
~ narration_1
"Three times Randolph Carter dreamed of the marvellous city, and three times was he snatched away while still he paused on the high terrace above it."
=> END

~ narration_2
"He resolved to find the gods on unknown Kadath in the cold waste, wherever that might be, and to win from them the sight and remembrance and shelter of the marvellous sunset city."
=> END

~ entrance_ghoul1
Ghoul: Ough! Not how I imagine arrival...
=> END

~ entrance_ghoul2
Ghoul: Ugh... What happen? Where others?
=> END

~ entrance_pickman
Pickman: Everyone else is gone...
=> END

~ entrance_carter_1
Carter: At last, we have arrived. Let us finish this.
Pickman: Still want to do this?
=> END

~ entrance_carter_2
Carter: I must.
=> END

~ starting_instructions
Move around with [ {ui_up} ]/[ {ui_left} ]/[ {ui_down} ]/[ {ui_right} ]. You can interact with individuals and objects with [ {ui_accept} ], and cancel with [ {ui_cancel} ]. Run with [ {action_run} ]
=> END

~ idle_pickman
Pickman: Go ahead. I'll join you once I've made sure these guys are OK. If you're in a hurry, use [ {action_run} ] to move faster.
=> END 

~ idle_left_ghoul
if EntitiesService.get_party().is_unlocked(PartyMember.Id.PICKMAN):
	Ghoul: We stay here. If you disoriented, open menu with [ {ui_menu} ] and check or change controls.
else:
	Ghoul: Everything still spinning... If you disoriented, open menu with [ {ui_menu} ] and check or change controls.
=> END

~ idle_right_ghoul
if EntitiesService.get_party().is_unlocked(PartyMember.Id.PICKMAN):
	Ghoul: Maybe others come. We wait for them. Be careful... Remember you can open menu with [ {ui_menu} ] and use items for heal.
else:
	Ghoul: One second we riding, next we thrown here. If you hurt, you can open menu with [ {ui_menu} ] and use items for heal.
=> END 

~ pickman_catch_up
Pickman: Wait!
=> END

~ pickman_recruit
Pickman: Looks like we're not alone.
Pickman: The others will stay at the entrance, but you'll have a better chance with me by your side.
=> END

~ pickman_join
Pickman joined your party!
=> END

~ after_boss
Carter: Let's go.
=> END

~ gods_callout
Carter: Gods of Earth! Cease your hiding and grant me passage to the Sunset City.
=> END

~ pickman_warning
Pickman: Wait. Something's wrong. The Other Gods, their messeng-
=> END

~ carter_call
Carter: Pickman!
=> END

~ nyarlathotep_1
????: Randolph Carter.
????: You have come to see the Great Ones, whom it is unlawful for men to see. 
????: Watchers have spoken of this thing, and the Other Gods have grunted as they rolled and tumbled mindlessly to the sound of thin flutes in the black ultimate void, where broods the daemon-sultan whose name no lips dare speak aloud.
????: Because of YOU, Earth has no longer any gods that are gods.
=> END

~ nyarlathotep_2
????: [shake rate=20.0 level=5 connected=1]And only the Other Gods from outer space hold sway on unremembered Kadath.[/shake]
=> END

# ===============
#  Interactions
# ===============


~ pushing_barrier
This is not the time to retreat. The journey to this place has been long and hard, and it's time to face the gods.
=> END

~ guard_ghoul
Ghoul: We'll guard the entrance... Y-You go ahead...
Ghoul: B-by the way, if you want to change the controls, you can open the menu with [{ui_menu}] and head to the Settings section.
=> END

~ guard_ghoul_2
Ghoul: So this is K-Kadath...? I-I think I'll stay here and guard the rear...
Ghoul: O-oh, remember you can change between walking and running by holding [{action_run}].
=> END

~ pickman_waiting
Pickman: Go ahead, then. We're still recovering from the landing.
=> END!

~ barrier
Carter: There must be a way to remove these. We should check the neighboring rooms.
=> END!

~ chalice
There is a curious glyph on the chalice. It seems to be pulsing with some sort of energy.

Touch the glyph?
- Yes
	do DialogueService.add_response("yes")
	You lean forward and touch the glyph.
- No
	do DialogueService.add_response("no")
	Maybe later.
=> END

~ after_chalice_activation
Something seems to have changed.
=> END

~ chalice_done
The chalice is filled with a red liquid.
=> END
